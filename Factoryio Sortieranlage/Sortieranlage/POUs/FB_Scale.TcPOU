<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_Scale" Id="{3695f822-08b1-4b68-8724-c691d102b138}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Scale EXTENDS FB_Conveyor
VAR_INPUT
    // Follower is ready to receive part
    bReleasePartTransfer: BOOL;
    // Part is at conveyor entry
    bPartAtEntry: BOOL;
    // Part is on scale
    bPartOnScale: BOOL;
    // Part is at conveyor exit
    bPartAtExit: BOOL;
    // Current weight at scale
    fWeightIn: REAL;
END_VAR
VAR_OUTPUT
    // Module is ready to receive part
    bReadyToReceivePart: BOOL;
    // Measurement data is refreshed in this cycle
    bWeightIsUpdated: BOOL;
    // Measurement data part weight
    fWeightOut: REAL;
END_VAR
VAR
    // Timeout before store measured weight of part
    fbWeightTimeout: TON;
    // Part is complete on distributor
    fbPartAtDistributor: F_TRIG();
    // Current state of scale
    eStateScale: (S_IDLE, S_MOVE_PART_TO_SCALE, S_WAIT_FOR_MEASUREMENT, S_EXECUTING_MEASUREMENT, S_WAIT_FOR_FOLLOWER, S_TRANSFER_TO_FOLLOWER, S_LOCKOUT);
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Cyclic calls of function block instances
SUPER^();
fbWeightTimeout();
fbPartAtDistributor(CLK := bPartAtExit);

// Default to measurement data has not been updated in this cycle
bWeightIsUpdated := FALSE;

// State machine: 
CASE eStateScale OF
    S_IDLE:
        SUPER^.Stop(bRunOut := TRUE);
        SUPER^.fbMovingTimeout.IN := FALSE;
        bReadyToReceivePart := TRUE;
        fbWeightTimeout.IN := FALSE;
        IF bPartAtEntry THEN
            bReadyToReceivePart := FALSE;
            eStateScale := S_MOVE_PART_TO_SCALE;
        END_IF

    S_MOVE_PART_TO_SCALE:
        SUPER^.Start();
        SUPER^.fbMovingTimeout.IN := TRUE;
        IF bPartOnScale THEN
            SUPER^.fbMovingTimeout.IN := FALSE;
            eStateScale := S_WAIT_FOR_MEASUREMENT;
        END_IF
        IF SUPER^.fbMovingTimeout.Q THEN
            {info 'ToDo: throw error message: Part moving timeout'}
            SUPER^.fbMovingTimeout.IN := FALSE;
            eStateScale := S_IDLE;
        END_IF
 
    S_WAIT_FOR_MEASUREMENT:
        SUPER^.Stop(bRunOut := FALSE);
        fbWeightTimeout.IN := TRUE;
        IF fbWeightTimeout.Q THEN
            fbWeightTimeout.IN := FALSE;
            eStateScale := S_EXECUTING_MEASUREMENT;
        END_IF
        IF NOT bPartOnScale THEN
            {info 'ToDo: throw error message: Part has been removed from scale'}
            fbWeightTimeout.IN := FALSE;
            eStateScale := S_IDLE;
        END_IF

    S_EXECUTING_MEASUREMENT:
        IF NOT bPartOnScale THEN
            {info 'ToDo: throw error message: Part has been removed from scale'}
            eStateScale := S_IDLE;
        ELSE
            bWeightIsUpdated := TRUE;
            fWeightOut := fWeightIn;
            IF bReleasePartTransfer THEN
                eStateScale := S_TRANSFER_TO_FOLLOWER;
            ELSE
                eStateScale := S_WAIT_FOR_FOLLOWER;
            END_IF
        END_IF

    S_WAIT_FOR_FOLLOWER:
        IF bReleasePartTransfer THEN
            eStateScale := S_TRANSFER_TO_FOLLOWER;
        END_IF

    S_TRANSFER_TO_FOLLOWER:
        SUPER^.Start();
        SUPER^.fbMovingTimeout.IN := TRUE;
        IF fbPartAtDistributor.Q THEN
            SUPER^.fbMovingTimeout.IN := FALSE;
            eStateScale := S_IDLE;
        END_IF
        IF SUPER^.fbMovingTimeout.Q THEN
            {info 'ToDo: throw error message: Part moving timeout'}
            SUPER^.fbMovingTimeout.IN := FALSE;
            eStateScale := S_IDLE;
        END_IF
END_CASE
]]></ST>
    </Implementation>
    <Property Name="tWeightTimeout" Id="{124ea750-2d7e-42e4-b4a8-045258bc5dca}">
      <Declaration><![CDATA[// Timeout before store measured weight of part
{attribute 'monitoring' := 'call'}
PROPERTY tWeightTimeout : TIME
]]></Declaration>
      <Get Name="Get" Id="{4045a908-c0fe-448a-b322-dba64860193b}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[tWeightTimeout := fbWeightTimeout.PT;
]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{367c91d9-efff-434d-b247-9499980309f5}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[fbWeightTimeout.PT := tWeightTimeout;
]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>