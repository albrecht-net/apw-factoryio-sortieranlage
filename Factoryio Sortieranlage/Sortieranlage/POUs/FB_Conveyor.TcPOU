<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_Conveyor" Id="{245a5808-fce9-4686-b31f-3aa50bc94906}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK ABSTRACT FB_Conveyor
VAR_INPUT
    // Machine control
    stMachineControl: ST_MachineControl;
END_VAR
VAR_OUTPUT
    // Conveyor is not ready and locked
    bError: BOOL;
    // Output to conveyor motor
    bConveyorOn: BOOL;
END_VAR
VAR
    // Part moving timeout
    fbMovingTimeout: TON;
    // Run-down-timeout after normal stop
    fbRunDownTimeout: TOF;
    // Current state of conveyor
    eStateConveyor: E_ConveyorState;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Cyclic call of timer for moving timeout in derived function block
fbMovingTimeout();

// Cyclic call of timer for run-down-timeout after normal stop
IF fbRunDownTimeout.PT > T#0S THEN
    fbRunDownTimeout();
END_IF

// Emergency stop is active
IF NOT stMachineControl.bEmergStopNC THEN
    eStateConveyor := E_ConveyorState.LOCKOUT;
END_IF


CASE eStateConveyor OF
    E_ConveyorState.IDLE:
        bConveyorOn := FALSE;
        fbRunDownTimeout.IN := FALSE;

    E_ConveyorState.RUNNING:
        bConveyorOn := TRUE;
        fbRunDownTimeout.IN := TRUE;

    E_ConveyorState.RUNNING_OUT:
        fbRunDownTimeout.IN := FALSE;
        IF NOT fbRunDownTimeout.Q OR fbRunDownTimeout.PT = T#0S THEN
            eStateConveyor := E_ConveyorState.IDLE;
        END_IF

    E_ConveyorState.LOCKOUT:
        bConveyorOn := FALSE;
        fbRunDownTimeout.IN := FALSE;
        bError := TRUE;
END_CASE
]]></ST>
    </Implementation>
    <Property Name="eConveyorState" Id="{c3c22aa1-1a18-495f-91a9-7da878b9c7e7}">
      <Declaration><![CDATA[// Current state of conveyor
{attribute 'monitoring' := 'variable'}
PROPERTY eConveyorState: E_ConveyorState
]]></Declaration>
      <Get Name="Get" Id="{1445cc84-0e35-4174-91ff-eebc02a4d2d3}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[eConveyorState := eStateConveyor;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="eModuleState" Id="{74c927eb-91d7-4392-ab5b-d53da1471de4}">
      <Declaration><![CDATA[// Current state of the module
PROPERTY ABSTRACT eModuleState: E_ModuleState
]]></Declaration>
      <Get Name="Get" Id="{7747a88a-9ba4-4fd0-9a75-7ce9bf665dac}">
        <Declaration><![CDATA[]]></Declaration>
      </Get>
    </Property>
    <Method Name="Reset" Id="{c6f9219d-30dc-4aaa-a52b-80bf5ad63bc8}">
      <Declaration><![CDATA[METHOD PUBLIC Reset
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF stMachineControl.bEmergStopNC AND eStateConveyor = E_ConveyorState.LOCKOUT THEN
    bError := FALSE;
    eStateConveyor := E_ConveyorState.IDLE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Start" Id="{e5324438-ffcb-4c49-a5d6-8babf4557efa}">
      <Declaration><![CDATA[METHOD PROTECTED Start
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF eStateConveyor = E_ConveyorState.IDLE OR eStateConveyor = E_ConveyorState.RUNNING_OUT THEN
    eStateConveyor := E_ConveyorState.RUNNING;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Stop" Id="{497f56fe-7509-4e86-b27b-bdcc17baac39}">
      <Declaration><![CDATA[METHOD PROTECTED Stop
VAR_INPUT
    // Enable timeout before conveyor halt
    bRunOut: BOOL := FALSE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF eStateConveyor = E_ConveyorState.RUNNING THEN
    // Stop converyor with timeout before halt
    IF fbRunDownTimeout.PT > T#0S AND bRunOut THEN
        eStateConveyor := E_ConveyorState.RUNNING_OUT;

    // Stop conveyor immediately
    ELSE
        eStateConveyor := E_ConveyorState.IDLE;
    END_IF
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Property Name="tMovingTimeout" Id="{d6846ea9-b660-4c68-926b-4c307d706c12}">
      <Declaration><![CDATA[// Part moving timeout
{attribute 'monitoring' := 'call'}
PROPERTY PUBLIC tMovingTimeout: TIME
]]></Declaration>
      <Get Name="Get" Id="{ef2e8eb8-38a9-4c47-a0e6-f0b2558ec260}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[tMovingTimeout := fbMovingTimeout.PT;
]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{37f62f04-9445-4ecc-aab4-6c0bc2cfc248}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[fbMovingTimeout.PT := tMovingTimeout;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="tRunDownTimeout" Id="{4b764709-8d3d-4944-95b7-04a2d130102e}">
      <Declaration><![CDATA[// Run-down-timeout after normal stop
{attribute 'monitoring' := 'call'}
PROPERTY PUBLIC tRunDownTimeout: TIME
]]></Declaration>
      <Get Name="Get" Id="{245e477e-a008-409d-aec7-abdc2592ce40}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[tRunDownTimeout := fbRunDownTimeout.PT;
]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{5476210a-bee1-4eef-b655-9d8c659c6410}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[fbRunDownTimeout.PT := tRunDownTimeout;
]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>