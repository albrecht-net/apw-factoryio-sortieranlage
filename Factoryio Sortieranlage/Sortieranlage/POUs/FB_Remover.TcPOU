<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_Remover" Id="{0bc5e74d-e01a-4eac-8057-39627e6c4303}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Remover EXTENDS FB_Conveyor
VAR_INPUT
    // Part is at conveyor entry
    bPartAtEntry: BOOL;
    // Part is at conveyor exit
    bPartAtExit: BOOL;
END_VAR
VAR_OUTPUT
    // Module is ready to receive part
    bReadyToReceivePart: BOOL;
    // Remover processed one part
    bPartProcessed: BOOL;
END_VAR
VAR
    // Part moving timeout
    fbMovingTimeout: TON;
    // Part entering remover conveyor
    fbPartAtEntry: R_TRIG();
    // Count parts entering remover conveyor
    nPartsOnConveyor: INT;
    // Current state of distributor
    eStateRemover: (R_IDLE, R_MOVE_PART, R_TRANSFER_TO_FOLLOWER, R_EMPTY_CONVEYOR, R_LOCKOUT);
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Cyclic calls of function block instances
SUPER^();
fbPartAtEntry(CLK := bPartAtEntry);

// Count parts entering remover conveyor
IF fbPartAtEntry.Q THEN
    nPartsOnConveyor := nPartsOnConveyor + 1;
END_IF

// State machine: remover
CASE eStateRemover OF
    R_IDLE:
        SUPER^.Stop(bRunOut := TRUE);
        SUPER^.fbMovingTimeout.IN := FALSE;
        bReadyToReceivePart := TRUE;
        IF nPartsOnConveyor > 0 THEN
            eStateRemover := R_MOVE_PART;
        END_IF

    R_MOVE_PART:
        SUPER^.Start();
        SUPER^.fbMovingTimeout.IN := TRUE;
        IF PartAtExit() THEN
            SUPER^.fbMovingTimeout.IN := FALSE;
        END_IF
        IF SUPER^.fbMovingTimeout.Q THEN
            {info 'ToDo: throw error message: Part moving timeout'}
            SUPER^.fbMovingTimeout.IN := FALSE;
            // Reset counter to 0 is moving timeout occurred
            nPartsOnConveyor := 0;
            eStateRemover := R_IDLE;
        END_IF

    R_TRANSFER_TO_FOLLOWER:
        SUPER^.Start();
        SUPER^.fbMovingTimeout.IN := TRUE;
        IF NOT bPartAtExit THEN
            SUPER^.fbMovingTimeout.IN := FALSE;
            // Decrease counter by one part
            nPartsOnConveyor := nPartsOnConveyor - 1;
            IF nPartsOnConveyor > 0 THEN
                eStateRemover := R_MOVE_PART;
            ELSE
                nPartsOnConveyor := 0;
                eStateRemover := R_IDLE;
            END_IF
        END_IF
        IF SUPER^.fbMovingTimeout.Q THEN
            {info 'ToDo: throw error message: Part transfer moving timeout'}
            SUPER^.fbMovingTimeout.IN := FALSE;
            // Reset counter to 0 is moving timeout occurred
            nPartsOnConveyor := 0;
            eStateRemover := R_IDLE;
        END_IF
END_CASE
]]></ST>
    </Implementation>
    <Method Name="PartAtExit" Id="{819b3435-f3e7-4c82-a928-abcde45f2cf7}">
      <Declaration><![CDATA[// Return TRUE if part reach conveyor exit and state machine will go to next step
METHOD PRIVATE PartAtExit: BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF bPartAtExit THEN
    eStateRemover := R_TRANSFER_TO_FOLLOWER;
ELSE
    PartAtExit := FALSE;
    RETURN;
END_IF

PartAtExit := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="tMovingTimeout" Id="{2efb2728-9f0d-43d3-862b-fd68db65250b}">
      <Declaration><![CDATA[// Maximum duration for part to reach exit of conveyor
{attribute 'monitoring' := 'call'}
PROPERTY PUBLIC tMovingTimeout : TIME
]]></Declaration>
      <Get Name="Get" Id="{36db0884-f442-4f2e-9856-af33307f7eb1}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[tMovingTimeout := fbMovingTimeout.PT;
]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{1a320144-ae0f-4a01-baec-2e883f795487}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[fbMovingTimeout.PT := tMovingTimeout;
]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>