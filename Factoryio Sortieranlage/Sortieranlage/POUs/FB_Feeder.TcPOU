<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_Feeder" Id="{c5d8e8bc-afa3-42ac-9bb3-50daf9da39ed}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Feeder EXTENDS FB_Conveyor
VAR_INPUT
    // Follower is ready to receive part
    bReleasePartTransfer: BOOL;
    // Part is at conveyor exit
    bPartAtExit: BOOL;
END_VAR
VAR_OUTPUT
    // Enable part emitter
    bEmitter: BOOL;
END_VAR
VAR
    // Enable processing of parts
    bEnable: BOOL;
    // Current state of feeder
    eStateFeeder: (F_IDLE, F_MOVE_PART, F_WAIT_FOR_FOLLOWER, F_TRANSFER_TO_FOLLOWER, F_EMPTY_CONVEYOR, F_LOCKOUT);
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Cyclic calls of function block instances
SUPER^();

// State machine: feeder
CASE eStateFeeder OF
    F_IDLE:
        SUPER^.Stop(bRunOut := FALSE);
        SUPER^.fbMovingTimeout.IN := FALSE;
        IF bEnable THEN
            bEmitter := TRUE;
            eStateFeeder := F_MOVE_PART;
        ELSE
            bEmitter := FALSE;
        END_IF

    F_MOVE_PART:
        SUPER^.Start();
        SUPER^.fbMovingTimeout.IN := TRUE;
        IF PartAtExit() THEN
            SUPER^.fbMovingTimeout.IN := FALSE;
        END_IF
        IF SUPER^.fbMovingTimeout.Q THEN
            {info 'ToDo: throw error message: Part moving timeout'}
            SUPER^.fbMovingTimeout.IN := FALSE;
            eStateFeeder := F_IDLE;
        END_IF

    F_WAIT_FOR_FOLLOWER:
        SUPER^.Stop(bRunOut := FALSE);
        IF NOT bPartAtExit THEN
            {info 'ToDo: throw message: Part was manual removed'}
            eStateFeeder := F_IDLE;
        END_IF
        IF bReleasePartTransfer THEN
            eStateFeeder := F_TRANSFER_TO_FOLLOWER;
        END_IF

    F_TRANSFER_TO_FOLLOWER:
        SUPER^.Start();
        SUPER^.fbMovingTimeout.IN := TRUE;
        IF NOT bPartAtExit THEN
            SUPER^.fbMovingTimeout.IN := FALSE;
            IF bEnable THEN
                eStateFeeder := F_MOVE_PART;
            ELSE
                eStateFeeder := F_EMPTY_CONVEYOR;
            END_IF
        END_IF
        IF SUPER^.fbMovingTimeout.Q THEN
            {info 'ToDo: throw error message: Part transfer moving timeout'}
            SUPER^.fbMovingTimeout.IN := FALSE;
            eStateFeeder := F_IDLE;
        END_IF

    F_EMPTY_CONVEYOR:
        bEmitter := FALSE;
        SUPER^.fbMovingTimeout.IN := TRUE;
        IF PartAtExit() THEN
            SUPER^.fbMovingTimeout.IN := FALSE;
        END_IF
        IF SUPER^.fbMovingTimeout.Q THEN
            SUPER^.fbMovingTimeout.IN := FALSE;
            eStateFeeder := F_IDLE;
        END_IF
END_CASE

// Unset enable
bEnable := FALSE;
]]></ST>
    </Implementation>
    <Method Name="Enable" Id="{5968398c-874e-4b04-a814-7d741cce27ab}">
      <Declaration><![CDATA[// Enable processing of parts
METHOD Enable: BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[bEnable := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="PartAtExit" Id="{c7305287-8257-4d92-8802-940a28b7e430}">
      <Declaration><![CDATA[// Return TRUE if part reach conveyor exit and state machine will go to next step
METHOD PRIVATE PartAtExit: BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF bPartAtExit AND bReleasePartTransfer THEN
    eStateFeeder := F_TRANSFER_TO_FOLLOWER;
ELSIF bPartAtExit THEN
    eStateFeeder := F_WAIT_FOR_FOLLOWER;
ELSE
    PartAtExit := FALSE;
    RETURN;
END_IF

PartAtExit := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="tMovingTimeout" Id="{29a45c02-ab1e-4262-8ddc-dfbb68084338}">
      <Declaration><![CDATA[// Part moving timeout
{attribute 'monitoring' := 'call'}
PROPERTY PUBLIC tMovingTimeout : TIME
]]></Declaration>
      <Get Name="Get" Id="{4a60306e-e9b7-4682-8cbb-3a15601b517d}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[tMovingTimeout := fbMovingTimeout.PT;
]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{ec94b853-c8d5-4b26-9137-79b8d1d43967}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[fbMovingTimeout.PT := tMovingTimeout;
]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>